import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")

static_library("wallet_connect") {
  sources = [
    "wallet_connect_utils.cc",
    "wallet_connect_utils.h",
  ]

  deps = [
    ":encryptor",
    ":generated_payload_types",
    ":mojom",
    "//base",
    "//url",
  ]
}

source_set("encryptor") {
  sources = [
    "encryptor.cc",
    "encryptor.h",
  ]

  deps = [
    ":generated_payload_types",
    "//base",
    "//crypto",
    "//third_party/boringssl",
  ]
}

mojom("mojom") {
  generate_java = true
  sources = [ "wallet_connect.mojom" ]
  public_deps = [ "//url/mojom:url_mojom_gurl" ]
}

generated_types("generated_payload_types") {
  sources = [ "wallet_connect.idl" ]
  root_namespace = "wallet_connect::%(namespace)s"
  deps = [ "//base" ]
  visibility = [
    ":encryptor",
    ":unit_tests",
    ":wallet_connect",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "encryptor_unittest.cc",
    "wallet_connect_utils_unittest.cc",
  ]

  deps = [
    ":encryptor",
    ":generated_payload_types",
    ":wallet_connect",
    "//base",
    "//crypto",
    "//testing/gtest",
  ]
}
